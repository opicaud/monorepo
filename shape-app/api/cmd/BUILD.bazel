load("@io_bazel_rules_go//go:def.bzl", "go_binary", "go_library", "go_test")
load("@rules_pact//:defs.bzl", "pact_test", "provider", "side_car")
load("@rules_oci//oci:defs.bzl", "oci_image", "oci_push", "oci_tarball")
load("@bazel_skylib//rules:write_file.bzl", "write_file")
load("@aspect_bazel_lib//lib:expand_template.bzl", "expand_template")

pact_test(
    name = "pact_test",
    testonly = True,
    consumer = "//shape-app/api/pacts/cli:grpc-consumer-go",
    provider = ":area-calculator-provider",
)

provider(
    name = "area-calculator-provider",
    srcs = ":cmd",
    opts = {
        "transport": "grpc",
        "port": "50051",
    },
    visibility = ["//visibility:public"],
    deps = [":proxy"],
)

side_car(
    name = "proxy",
    srcs = "//shape-app/api/proxy",
    health_check = "http://localhost:8080/healthz",
)

go_library(
    name = "cmd_lib",
    srcs = ["main.go"],
    importpath = "github.com/opicaud/monorepo/shape-app/api/cmd",
    visibility = ["//visibility:private"],
    deps = [
        "//cqrs/pkg/v3beta1",
        "//grpc-eventstore/v2beta1/cmd",
        "//shape-app/api/proto",
        "//shape-app/domain/pkg",
        "@com_github_grpc_ecosystem_go_grpc_middleware_v2//interceptors/logging",
        "@io_opentelemetry_go_contrib_instrumentation_google_golang_org_grpc_otelgrpc//:otelgrpc",
        "@io_opentelemetry_go_otel//:otel",
        "@io_opentelemetry_go_otel//propagation",
        "@io_opentelemetry_go_otel_sdk//trace",
        "@io_opentelemetry_go_otel_trace//:trace",
        "@org_golang_google_grpc//:go_default_library",
        "@org_golang_google_grpc//codes",
        "@org_golang_google_grpc//health/grpc_health_v1",
        "@org_golang_google_grpc//status",
        "@org_golang_x_exp//slog",
    ],
)

go_binary(
    name = "cmd",
    embed = [":cmd_lib"],
    visibility = ["//visibility:private"],
)

go_binary(
    name = "cmd_linux",
    embed = [":cmd_lib"],
    goarch = "amd64",
    goos = "linux",
    visibility = ["//visibility:private"],
)

filegroup(
    name = "config",
    srcs = [
        "config.yml",
    ],
    visibility = ["//visibility:public"],
)

go_test(
    name = "cmd_test",
    srcs = ["main_test.go"],
    embed = [":cmd_lib"],
    deps = [
        "//shape-app/domain/test",
        "@com_github_stretchr_testify//assert",
        "@com_github_stretchr_testify//mock",
        "@org_golang_google_grpc//:go_default_library",
        "@org_golang_google_grpc//credentials/insecure",
        "@org_golang_google_grpc//health/grpc_health_v1",
    ],
)

load("@rules_helm//helm:defs.bzl", "helm_chart", "helm_lint_test")
load("@container_structure_test//:defs.bzl", "container_structure_test")
load("@rules_pkg//:pkg.bzl", "pkg_tar")

pkg_tar(
    name = "tar",
    srcs = [":cmd_linux"],
)

oci_image(
    name = "oci_cmd_image",
    base = "@distroless_go",
    cmd = ["./cmd_linux"],
    tars = [":tar"],
)

oci_tarball(
    name = "tarball",
    image = ":oci_cmd_image",
    repo_tags = ["shapeapp:latest"],
)

write_file(
    name = "tags_tmpl",
    out = "tags.txt.tmpl",
    content = [
        "BUILD_VERSION",
    ],
)

expand_template(
    name = "stamped",
    out = "_stamped.tags.txt",
    stamp_substitutions = {"BUILD_VERSION": "{{STABLE_SHAPE_APP_NEXT_RELEASE_VERSION}}"},
    substitutions = {"BUILD_VERSION": "v0.0.0"},
    template = "tags_tmpl",
)

oci_push(
    name = "oci_cmd_image.push",
    image = ":oci_cmd_image",
    remote_tags = ":stamped",
    repository = "ghcr.io/opicaud/grpc_shape_app",
)
