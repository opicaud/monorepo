load("@bazel_gazelle//:def.bzl", "gazelle")
load("@aspect_bazel_lib//lib:transitions.bzl", "platform_transition_filegroup")
load("@rules_pkg//pkg:zip.bzl", "pkg_zip")

# gazelle:prefix github.com/opicaud/monorepo/pact-helper

gazelle(name = "gazelle")

gazelle(
    name = "gazelle-update-repos",
    args = [
        "-from_file=pact-helper/go.mod",
        "-to_macro=monorepo-deps.bzl%go_dependencies",
    ],
    command = "update-repos",
)

genrule(
    name = "rename_protobuf_plugin_bin",
    srcs = ["@pact_plugins//:pact_protobuf_plugin_bin"],
    outs = ["pact-protobuf-plugin"],
    cmd_bash = "cp $< $@",
    visibility = ["//visibility:private"],
)

platform(
    name = "x86_64-linux",
    constraint_values = [
        "@platforms//os:linux",
        "@platforms//cpu:x86_64",
    ],
)

config_setting(
    name = "conf_x86_64-macos",
    constraint_values = [
        "@platforms//os:osx",
        "@platforms//cpu:x86_64",
    ],
)

config_setting(
    name = "conf_x86_64-linux",
    constraint_values = [
        "@platforms//os:linux",
        "@platforms//cpu:x86_64",
    ],
)

filegroup(
    name = "pact_protobuf_plugin_bin_linux_x86_64",
    srcs = select({
        ":conf_x86_64-linux": ["@pact_plugins//:pact_protobuf_plugin_bin"],
        ":conf_x86_64-macos": [":compatible_pact_protobuf_plugin_bin_linux_x86_64"],
    }),
    visibility = ["//visibility:public"],
)

filegroup(
    name = "pact_verifier_cli_linux_x86_64",
    srcs = select({
        ":conf_x86_64-linux": ["@pact_reference//:pact_verifier_cli"],
        ":conf_x86_64-macos": [":compatible_pact_verifier_cli_linux_x86_64"],
    }),
    visibility = ["//visibility:public"],

)

platform_transition_filegroup(
    name = "compatible_pact_protobuf_plugin_bin_linux_x86_64",
    srcs = ["@pact_plugins//:pact_protobuf_plugin_bin"],
    target_platform = "x86_64-linux",
    visibility = ["//visibility:private"],
)

platform_transition_filegroup(
    name = "compatible_pact_verifier_cli_linux_x86_64",
    srcs = ["@pact_reference//:pact_verifier_cli"],
    target_platform = "x86_64-linux",
    visibility = ["//visibility:private"],
)

pkg_zip(
    name = "pact_plugins_zip",
    srcs = [
        ":rename_protobuf_plugin_bin",
        "@pact_plugins//:pact-plugin",
    ],
    package_dir = "pact.plugins.protobuf@v0.3.1",
    visibility = ["//visibility:public"],
)
