load("@io_bazel_rules_go//go:def.bzl", "go_binary", "go_library", "go_test")
load("@rules_oci//oci:defs.bzl", "oci_image", "oci_push", "oci_tarball")
load("@bazel_skylib//rules:write_file.bzl", "write_file")
load("@aspect_bazel_lib//lib:expand_template.bzl", "expand_template")
load("@container_structure_test//:defs.bzl", "container_structure_test")
load("@rules_pkg//:pkg.bzl", "pkg_tar")
load("@rules_pact//:defs.bzl", "pact_test", "provider")

provider(
    name = "api-grpc-eventstore",
    srcs = ":server",
    opts = {
        "transport": "grpc",
        "port": "50052",
    },
)

pact_test(
    name = "pact_test",
    testonly = True,
    consumer = "//cqrs/v3/pkg/pacts:cqrs-save-events",
    provider = ":api-grpc-eventstore",
)

go_library(
    name = "server_lib",
    srcs = ["main.go"],
    importpath = "github.com/opicaud/monorepo/grpc-eventstore/v2/inmemory/server",
    visibility = ["//visibility:private"],
    deps = [
        "//grpc-eventstore/v2/inmemory/server/config",
        "//grpc-eventstore/v2/proto",
        "@com_github_grpc_ecosystem_go_grpc_middleware_v2//interceptors/logging",
        "@io_opentelemetry_go_contrib_instrumentation_google_golang_org_grpc_otelgrpc//:otelgrpc",
        "@io_opentelemetry_go_otel//:otel",
        "@io_opentelemetry_go_otel//propagation",
        "@io_opentelemetry_go_otel_exporters_otlp_otlptrace_otlptracegrpc//:otlptracegrpc",
        "@io_opentelemetry_go_otel_sdk//trace",
        "@io_opentelemetry_go_otel_trace//:trace",
        "@org_golang_google_grpc//:go_default_library",
        "@org_golang_google_grpc//codes",
        "@org_golang_google_grpc//health/grpc_health_v1",
        "@org_golang_google_grpc//status",
        "@org_golang_x_exp//slog",
    ],
)

go_binary(
    name = "server",
    embed = [":server_lib"],
    visibility = ["//visibility:public"],
)

go_test(
    name = "server_test",
    srcs = ["main_test.go"],
    embed = [":server_lib"],
    deps = [
        "//cqrs/v3/pkg",
        "//grpc-eventstore/v2/inmemory/client",
        "//grpc-eventstore/v2/inmemory/pkg",
        "@com_github_google_uuid//:uuid",
        "@com_github_stretchr_testify//assert",
        "@com_github_stretchr_testify//suite",
        "@org_golang_google_grpc//health/grpc_health_v1",
    ],
)

go_binary(
    name = "server_linux",
    embed = [":server_lib"],
    goarch = "amd64",
    goos = "linux",
    visibility = ["//visibility:public"],
)

pkg_tar(
    name = "tar",
    srcs = [":server_linux"],
)

oci_image(
    name = "oci_cmd_image",
    base = "@distroless_go",
    cmd = ["./server_linux"],
    tars = [":tar"],
)

container_structure_test(
    name = "oci_cmd_test",
    configs = ["oci_cmd_test.yaml"],
    image = ":oci_cmd_image",
)

oci_tarball(
    name = "tarball",
    image = ":oci_cmd_image",
    repo_tags = ["eventstore:latest"],
)

write_file(
    name = "tags_tmpl",
    out = "tags.txt.tmpl",
    content = [
        "BUILD_VERSION",
    ],
)

expand_template(
    name = "stamped",
    out = "_stamped.tags.txt",
    stamp_substitutions = {"BUILD_VERSION": "{{STABLE_GRPC_EVENTSTORE_NEXT_RELEASE_VERSION}}"},
    substitutions = {"BUILD_VERSION": "v0.0.0"},
    template = "tags_tmpl",
)

oci_push(
    name = "oci_cmd_image.push",
    image = ":oci_cmd_image",
    remote_tags = ":stamped",
    repository = "ghcr.io/opicaud/grpc_inmemory_eventstore",
)
